start: HEAD
accept: AC
reject: RE
blank: *

HEAD 0 -> PREPARE_MUL9 0 <
HEAD 1 -> PREPARE_MUL9 1 <

PREPARE_MUL9 * -> PREPARE_MUL8 * <
PREPARE_MUL8 * -> PREPARE_MUL7 * <
PREPARE_MUL7 * -> PREPARE_MUL6 * <
PREPARE_MUL6 * -> PREPARE_MUL5 * <
PREPARE_MUL5 * -> PREPARE_MUL4 * <
PREPARE_MUL4 * -> PREPARE_MUL3 * <
PREPARE_MUL3 * -> PREPARE_MUL2 * <
PREPARE_MUL2 * -> PREPARE_MUL1 * <
PREPARE_MUL1 * -> PREPARE_MUL @ <

PREPARE_MUL * -> CLONE 1 >

CLONE @ -> CLONE @ >
CLONE * -> CLONE * >
CLONE 0 -> CLONE2 0 ^
CLONE 1 -> CLONE2 1 ^
CLONE A -> CLONE2 A ^
CLONE B -> CLONE2 B ^
CLONE ! -> FACTORIAL_DONE * <

CLONE2 A -> CLONE2 A >
CLONE2 B -> CLONE2 B >
CLONE2 0 -> TRANSFER0 A <
CLONE2 1 -> TRANSFER1 B <
CLONE2 * -> NORMALIZE * <

FACTORIAL_DONE * -> FACTORIAL_DONE * <
FACTORIAL_DONE @ -> FACTORIAL_DONE2 * <

FACTORIAL_DONE2 0 -> FACTORIAL_DONE2 0 <
FACTORIAL_DONE2 1 -> FACTORIAL_DONE2 1 <
FACTORIAL_DONE2 * -> AC * >

TRANSFER0 * -> TRANSFER0 * <
TRANSFER0 A -> TRANSFER0 A <
TRANSFER0 B -> TRANSFER0 B <
TRANSFER0 @ -> PUT0 @ >
TRANSFER0 0 -> PUT0 0 >
TRANSFER0 1 -> PUT0 1 >

TRANSFER1 * -> TRANSFER1 * <
TRANSFER1 A -> TRANSFER1 A <
TRANSFER1 B -> TRANSFER1 B <
TRANSFER1 @ -> PUT1 @ >
TRANSFER1 0 -> PUT1 0 >
TRANSFER1 1 -> PUT1 1 >

PUT0 * -> CLONE 0 >
PUT1 * -> CLONE 1 >

NORMALIZE A -> NORMALIZE 0 <
NORMALIZE B -> NORMALIZE 1 <
NORMALIZE * -> PREPARE_DECREMENT * >

PREPARE_DECREMENT 0 -> PREPARE_DECREMENT 0 >
PREPARE_DECREMENT 1 -> PREPARE_DECREMENT 1 >
PREPARE_DECREMENT * -> DECREMENT * <

DECREMENT 0 -> DECREMENT 1 <
DECREMENT 1 -> PREPARE_TRIM 0 <

PREPARE_TRIM 0 -> PREPARE_TRIM 0 <
PREPARE_TRIM 1 -> PREPARE_TRIM 1 <
PREPARE_TRIM * -> TRIM * >

TRIM 0 -> TRIM * >
TRIM 1 -> PREPARE_MULTIPLICATION 1 <
TRIM * -> PREPARE_MULTIPLICATION ! <

PREPARE_MULTIPLICATION * -> PREPARE_MULTIPLICATION * <
PREPARE_MULTIPLICATION 0 -> PREPARE_MULTIPLICATION 0 <
PREPARE_MULTIPLICATION 1 -> PREPARE_MULTIPLICATION 1 <
PREPARE_MULTIPLICATION @ -> PREPARE_MULTIPLICATION2 @ <

PREPARE_MULTIPLICATION2 0 -> PREPARE_MULTIPLICATION2 0 <
PREPARE_MULTIPLICATION2 1 -> PREPARE_MULTIPLICATION2 1 <
PREPARE_MULTIPLICATION2 * -> PREPARE_MULTIPLICATION3 * <

PREPARE_MULTIPLICATION3 * -> PREPARE_MULTIPLICATION4 * <
PREPARE_MULTIPLICATION4 * -> PREPARE_MULTIPLICATION5 * <
PREPARE_MULTIPLICATION5 * -> PREPARE_MULTIPLICATION6 * <
PREPARE_MULTIPLICATION6 * -> PREPARE_MULTIPLICATION7 * <
PREPARE_MULTIPLICATION7 * -> PREPARE_MULTIPLICATION8 * <
PREPARE_MULTIPLICATION8 * -> PREPARE_MULTIPLICATION9 * <
PREPARE_MULTIPLICATION9 * -> PREPARE_MULTIPLICATION10 * <
PREPARE_MULTIPLICATION10 * -> PREPARE_MULTIPLICATION11 * <
PREPARE_MULTIPLICATION11 * -> PREPARE_MULTIPLICATION12 * <
PREPARE_MULTIPLICATION12 * -> PREPARE_MULTIPLICATION13 * <
PREPARE_MULTIPLICATION13 * -> PREPARE_MULTIPLICATION14 * <
PREPARE_MULTIPLICATION14 * -> PREPARE_MULTIPLICATION15 * <
PREPARE_MULTIPLICATION15 * -> PREPARE_MULTIPLICATION16 * <
PREPARE_MULTIPLICATION16 * -> PREPARE_MULTIPLICATION17 * <
PREPARE_MULTIPLICATION17 * -> PREPARE_MULTIPLICATION18 * <
PREPARE_MULTIPLICATION18 * -> PREPARE_MULTIPLICATION19 * <
PREPARE_MULTIPLICATION19 * -> PREPARE_MULTIPLICATION20 + <
PREPARE_MULTIPLICATION20 * -> ADD_MOVE 0 >

ADD_MOVE + -> ADD_MOVE + >
ADD_MOVE * -> ADD_MOVE * >
ADD_MOVE 0 -> ADD_TRANSFER0 * <
ADD_MOVE 1 -> ADD_TRANSFER1 * <
ADD_MOVE @ -> MUL @ >

ADD_TRANSFER0 0 -> ADD_PUT0 0 >
ADD_TRANSFER0 1 -> ADD_PUT0 1 >
ADD_TRANSFER0 + -> ADD_PUT0 + >
ADD_TRANSFER0 * -> ADD_TRANSFER0 * <

ADD_TRANSFER1 0 -> ADD_PUT1 0 >
ADD_TRANSFER1 1 -> ADD_PUT1 1 >
ADD_TRANSFER1 + -> ADD_PUT1 + >
ADD_TRANSFER1 * -> ADD_TRANSFER1 * <

ADD_PUT0 * -> ADD_MOVE 0 >
ADD_PUT1 * -> ADD_MOVE 1 >

MUL 0 -> MUL 0 >
MUL 1 -> MUL 1 >
MUL * -> _MUL * <

_MUL 0 -> MUL0 * <
_MUL 1 -> MUL1 * <
_MUL @ -> MUL_RELOAD @ <

MUL0 0 -> MUL0 0 <
MUL0 1 -> MUL0 1 <
MUL0 @ -> MUL0 @ <
MUL0 * -> _MUL0 * <

_MUL0 * -> _MUL0 * <
_MUL0 0 -> SHIFT0 0 >
_MUL0 1 -> SHIFT0 1 >

MUL1 0 -> MUL1 0 <
MUL1 1 -> MUL1 1 <
MUL1 @ -> MUL1 @ <
MUL1 * -> _MUL1 * <

_MUL1 * -> _MUL1 * <
_MUL1 0 -> SHIFT1 0 >
_MUL1 1 -> SHIFT1 1 >

SHIFT0 * -> ADD_MOVE 0 ^
SHIFT1 * -> SUM ! <

SUM 0 -> SUM 0 <
SUM 1 -> SUM 1 <
SUM + -> START + <

START * -> FIND * >
START 0 -> FIND A >
START 1 -> FIND B >

FIND 0 -> FIND 0 >
FIND 1 -> FIND 1 >
FIND + -> FIND + >
FIND * -> BACK * <
FIND ! -> BACK ! <
FIND X -> BACK X <
FIND Y -> BACK Y <

BACK 0 -> FOUND0 X <
BACK 1 -> FOUND1 Y <
BACK + -> ADD_NORMALIZE + <

FOUND0 0 -> FOUND0 0 <
FOUND0 1 -> FOUND0 1 <
FOUND0 + -> FOUND0 + <
FOUND0 A -> START 0 <
FOUND0 B -> START 1 <
FOUND0 * -> START 0 <

FOUND1 0 -> FOUND1 0 <
FOUND1 1 -> FOUND1 1 <
FOUND1 + -> FOUND1 + <
FOUND1 A -> START 1 <
FOUND1 B -> CARRY C <
FOUND1 * -> START 1 <

CARRY * -> CARRY_DONE 1 >
CARRY 0 -> CARRY_DONE 1 >
CARRY 1 -> CARRY 0 <

CARRY_DONE 0 -> CARRY_DONE 0 >
CARRY_DONE 1 -> CARRY_DONE 1 >
CARRY_DONE C -> START 0 <


ADD_NORMALIZE 0 -> ADD_NORMALIZE 0 <
ADD_NORMALIZE 1 -> ADD_NORMALIZE 1 <
ADD_NORMALIZE A -> ADD_NORMALIZE 0 <
ADD_NORMALIZE B -> ADD_NORMALIZE 1 <
ADD_NORMALIZE * -> ADD_NORMALIZE2 * >

ADD_NORMALIZE2 0 -> ADD_NORMALIZE2 0 >
ADD_NORMALIZE2 1 -> ADD_NORMALIZE2 1 >
ADD_NORMALIZE2 + -> ADD_NORMALIZE2 + >
ADD_NORMALIZE2 X -> ADD_NORMALIZE2 0 >
ADD_NORMALIZE2 Y -> ADD_NORMALIZE2 1 >
ADD_NORMALIZE2 ! -> ADD_NORMALIZE2 0 >
ADD_NORMALIZE2 * -> ADD_MOVE * >

MUL_RELOAD * -> MUL_RELOAD * <
MUL_RELOAD 0 -> MUL_RELOAD * <
MUL_RELOAD 1 -> MUL_RELOAD * <
MUL_RELOAD + -> MUL_RELOAD2 * <

MUL_RELOAD2 0 -> MUL_RELOAD_TRANSFER0 # >
MUL_RELOAD2 1 -> MUL_RELOAD_TRANSFER1 # >

MUL_RELOAD_TRANSFER0 * -> MUL_RELOAD_TRANSFER0 * >
MUL_RELOAD_TRANSFER0 @ -> MUL_RELOAD_PUT0 @ <
MUL_RELOAD_TRANSFER0 0 -> MUL_RELOAD_PUT0 0 <
MUL_RELOAD_TRANSFER0 1 -> MUL_RELOAD_PUT0 1 <

MUL_RELOAD_TRANSFER1 * -> MUL_RELOAD_TRANSFER1 * >
MUL_RELOAD_TRANSFER1 @ -> MUL_RELOAD_PUT1 @ <
MUL_RELOAD_TRANSFER1 0 -> MUL_RELOAD_PUT1 0 <
MUL_RELOAD_TRANSFER1 1 -> MUL_RELOAD_PUT1 1 <

MUL_RELOAD_PUT0 * -> MUL_RELOAD_REPEAT 0 <
MUL_RELOAD_PUT1 * -> MUL_RELOAD_REPEAT 1 <

MUL_RELOAD_REPEAT * -> MUL_RELOAD_REPEAT * <
MUL_RELOAD_REPEAT # -> MUL_RELOAD_CHECK * <

MUL_RELOAD_CHECK * -> MUL_DONE * >
MUL_RELOAD_CHECK 0 -> MUL_RELOAD_TRANSFER0 # >
MUL_RELOAD_CHECK 1 -> MUL_RELOAD_TRANSFER1 # >

MUL_DONE * -> MUL_DONE * >
MUL_DONE 0 -> MUL_DONE 0 >
MUL_DONE 1 -> MUL_DONE 1 >
MUL_DONE @ -> CLONE @ >

